PN KK

	TITL	'KMBG-KOMMENTARTEIL'
;************************************************************
;************************************************************
;**               **                                       **
;**   BAMOS       **   KASSETTENMAGNETBAND                 **
;**               **                                       **
;************************************************************
;** STAND:      01.01.81       OS-VERSION: V1.2/19.7.84    **
;** BEARBEITER: HILDEBRANDT/ AENDERUNG: TOCHATSCHEK        **
;************************************************************
;** GERAETEBEDIENROUTINE FUER KASSETTENMAGNETBAND          **
;** ANSCHLUSS VON ZWEI LAUFWERKEN K5200 AN EINER AKB K5020 **
;************************************************************
;************************************************************



;PROGRAMME DIE ZUR GERAETEBEDIENROUTINE GEHOEREN:

;   KK  KOMMENTARTEIL MIT GENERIERPARAMETERN
;   KP  INITIALISIEREN DER AKB
;   KS  STEUERTEIL DER BEDIENROUTINE
;   KI  INTERRUPTSERVICEROUTINEN
;   KU  UNTERPROGRAMME
;   KC  CTC-ROUTINE ZUR ZEITSTEUERUNG
;   KF  FEHLERBEHANDLUNG
;   KA  ARBEITSSPEICHER (RAM)
;   KT  TABELLE DER ADRESSEN DER ISR AKB
;   KV  TABELLE DER ADRESSEN DER ISR CTC
;*********************************************************************
;E/A-TABELLE
;BYTE 0    STATUS
;BYTE 1    FEHLERSCHLUESSEL
;BYTE 2    BASISADRESSE DER AKB
;BYTE 3    SUBADRESSE
;BYTE 4    KOMMANDO
;BYTE 5    L-TEIL ADRESSE EINTRITTSPUNKT
;BYTE 6    H-TEIL ADRESSE EINTRITTSPUNKT
;BYTE 7    L-TEIL BLOCKADRESSE
;BYTE 8    H-TEIL BLOCKADRESSE
;BYTE 9    H-TEIL BLOCKLAENGE (BIT 15,...,BIT 8)
;BYTE 10   L-TEIL BLOCKLAENGE (BIT 7,...,BIT 0), N BEI SUCHEN BANDMARKE
;BYTE 11   ANZAHL LESE-/SCHREIB WIEDERHOLEN
;BYTE 12   ANZAHL BLOCKLUECKENVERLAENGERUNGEN
;*********************************************************************
;STATUS
;BIT 0     0 GERAET FREI
;          1 GERAET BESETZT
;BIT 1     0
;          1 DATENTRAEGERENDE
;BIT 2     0 AUFZEICHNEN ERLAUBT
;          1 AUFZEICHNEN VERBOTEN
;BIT 3     0 KASSETTENSEITE A
;          1 KASSETTENSEITE B
;BIT 4     0 GERAET BEREIT
;          1 GERAET NICHT BEREIT
;BIT 5     0 GERAET NICHT RESERVIERT
;          1 GERAET RESERVIERT
;BIT 6     0 AUFZEICHNUNGSVERFAHREN  38 CM*S**-1
;          1 AUFZEICHNUNGSVERFAHREN  19 CM*S**-1
;BIT 7     0 KOMMANDO FEHLERFREI AUSGEFUEHRT
;          1 FEHLER (SIEHE FEHLERSCHLUESSEL)
;*********************************************************************
;KOMMANDO (BIT 7=1 --> OHNE FEHLERWIEDERHOLUNG)
;  HEX   KOMMANDO
;  00    AUFZEICHNEN MIT RAW (2=<BLOCKLAENGE=<256)
;  08    AUFZEICHNEN OHNE RAW
;  02    WIEDERGABE (ES WIRD DER NAECHSTE BLOCK
;        GELESEN, ADRESSE UND LAENGE WERDEN IN DER
;        E/A-TABELLE UEBERMITTELT
;  11    EIN BLOCK VORSETZEN
;  15    EIN BLOCK RUECKSETZEN
;  21    UMSPULEN
;  31    RESERVIEREN EIN
;  41    RESERVIEREN AUS
;  51    SCHREIBEN EINER BANDMARKE (STEUERBLOCK)
;  61    SCHREIBE SCHLUSSLUECKE
;  71    SUCHE N-TE BANDMARKE VORWAERTS
;  75    SUCHE N-TE BANDMARKE RUECKWAERTS
;*********************************************************************
;FEHLERSCHLUESSEL
;   HEX
;   10        FALSCHES KOMMANDO
;   11        ANGEWAEHLTES GERAET NICHT RESERVIERT
;   12        SUBADRESSE FALSCH
;   13        PUFFERLAENGE KLEINER 12 ODER GROESSER 256
;   14        ENDE DER AUFZEICHNUNGEN AUF DIESER KASSETTENSEITE (LESEN)
;   15        ANGEWAEHLTES GERAET BESETZT
;   16        ENDE/ANFANG DER AUFZEICHNUNGEN (BANDMARKE SUCHEN)
;   17        GELESENER BLOCK LAENGER 260 BYTE
;   18        BANDMARKE NICHT GEFUNDEN
;   19        AUFZEICHNEN VERBOTEN(MIT KOMMANDO:AUFZEICHNEN)
;   21        FEHLER NACH FESTGELEGTER ANZAHL LESEWIEDERHOLUNGEN
;   22        FEHLER NACH FESTGELEGTER ANZAHL SCHREIBWIEDERHOLUNGEN
;   23        KEIN ECHOSIGNAL VOM EINGABEKANAL BEI RAW
;*********************************************************************

;HINWEISE ZUR ARBEIT MIT DER GERAETEBEDIENROUTINE
;  * IM2 EINSTELLEN,SP UND I-REGISTER LADEN
;  * E/A-TABELLE LADEN(BASISADRESSE AKB),ADRESSE E/A-TAB. IN IX LADEN
;  * PROGRAMMIEREN AKB DURCH AUFRUF DES UP'S KP.INIT (CALL KP.INIT)
;  * LADEN SCHLUESSEL FUER KOMMANDO,SUBADRESSE,ADRESSE EINTRITTSPUNKT
;  * AUFRUF GERAETEBEDIENROUTINE IMMER MIT CALL KS.BRKMB
;  * WIRD KEINE PARALLELARBEIT MIT ANDEREN PROGRAMMEN GEWUENSCHT,KANN
;    WIE IN FOLGENDEM BEISPIEL VERFAHREN WERDEN:
;         .
;         .
;         .
;         CALL KS.BRKMB ;AUFRUF
;   MARKE:HALT
;         JR   MARKE-#
;   ADREP:............  ;ADRESSE EINTRITTSPUNKT
;         .
;         .
;         .
;  * BEI RUECKKEHR DER BEDIENROUTINE ZUM EINTRITTSPUNKT IST DIE ADRESSE
;    DES FOLGENDEN BEFEHLS, BEI DEM DER LETZTE INTERRUPT ANGENOMMEN
;    WURDE, NOCH IM SP.WIRD, WIE IM OBIGEN BEISPIEL, DIESE ADRESSE NICH
;    MEHR BENOETIGT,SO IST DER SP UM 2 ZU ERHOEHEN.
;*********************************************************************
;*
;*            GENERIERINFORMATION
;*
;*********************************************************************
;CTCKA=0,1,2,3  CTC-KANAL
;CTCBA=BASISADRESSE CTC (STANDARD:80H)
CTCKA:	EQU	0		;CTC-KANAL 0
CTCBA:	EQU	80H		;BASISADRESSE CTC
	END	

PN KV02


	ORG	00248H
;************************************************************
;************************************************************
;**               **                                       **
;**   BAMOS       **   KASSETTENMAGNETBAND                 **
;**               **                                       **
;************************************************************
;** STAND:      01.01.81       OS-VERSION:  V1.2/19.7.84   **
;** BEARBEITER: HILDEBRANDT/ AENDERUNG: TOCHATSCHEK        **
;************************************************************
;**                                                        **
;** TABELLE DER ADRESSEN DER ISR CTC                       **
;************************************************************
;************************************************************
;FUER DEN CTC AUF DER ZRE-PLATTE SIND HIER
;DIE ADRESSEN DER INTERRUPTSERVICEROUTINEN FUER DIE
;EINZELNEN KANAELE (0,1,2,3) EINZUTRAGEN
;L-TEIL VON ICTC0:XXXXX000

ICTC0:	DA	KC.ICTC		;INT. BEH. CTC KANAL 0
ICTC1:	DA	0		;INT. BEH. CTC KANAL 1
ICTC2:	DA	0		;INT. BEH. CTC KANAL 2
ICTC3:	DA	0		;INT. BEH. CTC KANAL 3
	END	

PN KT


;************************************************************
;************************************************************
;**               **                                       **
;**   BAMOS       **   KASSETTENMAGNETBAND                 **
;**               **                                       **
;************************************************************
;** STAND:      01.01.81       OS-VERSION:   V1.2          **
;** BEARBEITER: HILDEBRANDT                                **
;************************************************************
;** ADRESSENVERBINDUNGSTABELLE ZU ISR                      **
;************************************************************
;************************************************************


IAUS:DA KI.IAUS;INT. BEH AUSGABEKANAL
IER:DA KI.IER;INT. BEH. EINGABEKANAL RAW
IEL:DA KI.IEL;INT. BEH. EINGABEKANAL LESEN
IST:DA KI.IST;INT. BEH. STATUS
IBE:DA KI.IBE;INT. BEH. BANDENDE
IBE0:DA KI.IBE0;BEHANDLUNG BLINDINTERRUPT
END 
PN KA02

	TITL	'KMBG-ARBEITSBEREICH'

	ORG	0DEE4H
;************************************************************
;************************************************************
;**               **                                       **
;**   BAMOS       **   KASSETTENMAGNETBAND                 **
;**               **                                       **
;************************************************************
;** STAND:      01.01.81       OS-VERSION:   V1.2          **
;** BEARBEITER: HILDEBRANDT                                **
;************************************************************
;** ARBEITSBEREICH ZUR GERAETEBEDIENROUTINE KMBG           **
;************************************************************


AEATB:	DA	0		;ADRESSE E/A-TABELLE
PZAUS:	DA	0		;PUFFERZEIGER AUSGABE
PZEIN:	DA	0		;PUFFERZEIGER EINGABE
ZAUS:	DA	0		;ZAEHLER AUSGABE
ZEIN:	DA	0		;ZAEHLER EINGABE
Z1:	DB	0		;ZAEHLER SCHREIB-/LESE WDHG.
Z2:	DB	0		;ZAEHLER BLOCKLUECKENVERL.
CRCZ:	DA	0		;CRC-ZEICHEN
FEHLZ:	DB	0		;INTERNER FEHLERANZEIGER
;***************************************************************
; BEDEUTUNG DES INTERNEN FEHLERANZEIGERS
;  BIT     FEHLER
;------------------------------------------------------------
;    0     KEIN ECHOSIGNAL VOM EINGABEKANAL BEI RAW
;    1     LETZTER INTERRUPT NICHT GEKOMMEN BEI RAW
;    2     STATUS WAEHREND DER DATENUEBERTRAGUNG
;    3     CRC-ZEICHEN FEHLERHAFT BEIM LESEN
;    4     RAW-FEHLER
;-----------------------------------------------------------
;***************************************************************
POS:	DA	0		;POSITION DES FEHLERS IM DATENSATZ
SDAT:	DB	0		;SOLLDATEN
IDAT:	DB	0		;ISTDATEN
PUFFS:	BER	260		;PUFFERSPEICHER
;ARBEITSZELLEN FUER CRC-ROUTINE
KOMDO:	DB	0		;KOMMANDO
ZEITK:	DB	0		;ZEITKONSTANTE
ADR:	DA	0		;ADRESSE
CONTR:	DB	0		;EREIGNISKONTROLLE
	END	

PN KS

	TITL	'KMBG-STEUERTEIL'

	ORG	0D260H
;************************************************************
;************************************************************
;**               **                                       **
;**   BAMOS       **   KASSETTENMAGNETBAND                 **
;**               **                                       **
;************************************************************
;** STAND:  01.03.83/19.07.84  OS-VERSION:   V1.3          **
;** BEARBEITER: HILDEBRANDT/ AENDERUNG: TOCHATSCHEK        **
;************************************************************
;** STEUERTEIL DER GERAETEBEDIENROUTINE                    **
;** FUER KASSETTENMAGNETBAND                               **
;************************************************************
;************************************************************


;-----------------------------------------------------------0010
BRKMB:	DI
	PUSH	IX		;REG.RETTEN
	CALL	KU.RRET
	LD	(KA.AEATB),IX	;ADR.E/A-TAB
;-----------------------------------------------------------0020
	BIT	0,(IX)
	JPNZ	STF06		;FEHLER,GERAET NICHT FREI
;-----------------------------------------------------------0030
	RES	7,(IX)		;FEHLERBIT
	LD	(IX+1),0	;FEHLERSCHLUESSEL
;-----------------------------------------------------------0040
STT00:	SET	0,(IX)		;BESETZTBIT
	EI
;-----------------------------------------------------------0050
	LD	A,(IX+3)	;SUBADR.PRUEFEN
	DEC	A
	JRZ	STT01-#
	DEC	A
	JPNZ	STF01		;SUBADRESSE FALSCH
;-----------------------------------------------------------0060
STT01:	LD	A,(IX+2)	;BASISADRESSE
	ADD	4
	LD	C,A		;REL.ADR.4
	LD	A,(IX+4)	;KOMMANDO RESERVIEREN EIN ?
	CMP	31H
	JPZ	STT30		;SPRUNG NACH RESERVIEREN EIN
;-----------------------------------------------------------0070
	CMP	41H		;KOMMANDO RESERVIEREN AUS ?
	JPZ	STT40		;SPRUNG NACH RESERVIEREN AUS
;-----------------------------------------------------------0080
;PRUFEN OB ADRESSIERTES GERAET RESERVIERT UND ANWAHL EINSCHALTEN
	IN	A
	AND	3		;RUECKSETZEN
	INC	C		;REL.ADR.5
	IN	B
	BIT	0,(IX+3)
	JRZ	STT02-#		;SPRUNG WENN LW2
	BIT	1,A
	JPNZ	STF02		;GERAET NICHT RESERVIERT
	BIT	4,B
	JPZ	STF03		;GERAET NICHT BEREIT
	SET	3,A		;ANWAHL GERAET 1
	JR	STT03-#
STT02:	BIT	0,A
	JPNZ	STF02		;GERAET NICHT RESERVIERT
	BIT	3,B
	JPZ	STF03		;GERAET NICHT BEREIT
	SET	2,A		;ANWAHL GERAET 2
STT03:	DEC	C		;REL.ADR.4
	OUT	A		;EINSCHALTEN ANWAHL
;-----------------------------------------------------------0090

;ANALYSE KOMMANDO

	LD	A,(IX+4)	;KOMMANDO
	AND	077H		;BIT 7,3 AUSBLENDEN
	JRZ	STT10-#		;SPRUNG ZUM AUFZEICHNUNGSZWEIG
;-----------------------------------------------------------0100
	CMP	02H
	JPZ	STT20		;SPRUNG ZUM EINGABEZWEIG
;-----------------------------------------------------------0110
	AND	0F0H
	CMP	10H
	JPZ	STT60		;BLOCK VOR-/RUECKSETZEN
;-----------------------------------------------------------0120
	CMP	20H
	JPZ	STT50		;UMSPULEN
;-----------------------------------------------------------0130
	CMP	50H
	JPZ	STT70		;SCHREIBEN EINER BANDMARKE
;-----------------------------------------------------------0140
	CMP	60H
	JPZ	STT80		;SCHREIBEN EINER SCHLUSSLUECKE
;-----------------------------------------------------------0150
	CMP	70H
	JPZ	STT90		;SUCHE BANDMARKE
;-----------------------------------------------------------0160
	JMP	STF07		;FEHLER: KEIN GUELTIGES KOMMANDO
;-----------------------------------------------------------0170
;AUFZEICHNUNGSZWEIG
STT10:	BIT	2,(IX)		;AUFZEICHNEN ERLAUBT ?
	JPNZ	STF0B		;SPRUNG WENN AUFZEICHNEN VERBOTEN
;-----------------------------------------------------------0180
	BIT	1,(IX)
	JRNZ	STT06-#
;-----------------------------------------------------------0190
	IN	D
	BIT	7,D		;SIGNAL AEB
	CAZ	KU.MBAN		;MB EINZIEHEN BEIM SCHREIBEN
;-----------------------------------------------------------0200
;PRUEFEN BLOCKLAENGE
STT06:	LD	B,(IX+9)
	LD	C,(IX+10)	;<BC>=BLOCKLAENGE
	LD	A,C
	OR	A
	LD	C,1
	JRZ	STT11-#		;L-TEIL = 0
	DEC	C
	CMP	12
	JRC	STFBL-#		;L-TEIL < 12
STT11:	LD	A,B		;H-TEIL
	CMP	C
STFBL:	JPNZ	STF04		;BLOCKLAENGE NICHT 12,...,256
;-----------------------------------------------------------0210
;PUFFERSPEICHER LADEN
STT12:	LD	HL,KA.PUFFS	;ADR. PUFFERSP.
	LD	M,0AAH		;PRAEAMBEL
	INC	HL
	PUSH	HL
	EX	DE,HL
	LD	L,(IX+7)
	LD	H,(IX+8)	;ADRESSE BLOCK
	LD	B,(IX+9)
	LD	C,(IX+10)
	LD	A,C
	LDIR
	LD	HL,0
	LD	(KA.CRCZ),HL
	POP	HL
	PUSH	DE
	LD	B,A
	CALL	KU.CRC		;BERECHNE CRC-ZEICHEN
	POP	HL
	LD	M,D		;1.BYTE CRC
	INC	HL
	LD	M,E		;2.BYTE CRC
	INC	HL
	LD	M,0AAH		;POSTAMBEL
;-----------------------------------------------------------0220

;ARBEITSBEREICH LADEN
	XOR	A
	LD	(KA.Z1),A
	LD	(KA.Z2),A
;-----------------------------------------------------------0230
STTEP:	LD	A,3		;EINSPRUNG BEI WIEDERHOLUNG
	LD	(KA.FEHLZ),A
	LD	HL,KA.PUFFS
	LD	(KA.PZAUS),HL
	LD	(KA.PZEIN),HL
	LD	H,(IX+9)
	LD	L,(IX+10)
	LD	DE,4
	ADD	HL,DE
	LD	(KA.ZEIN),HL
	LD	(KA.ZAUS),HL
;-----------------------------------------------------------0240
STT16:	LD	A,(IX+2)
	ADD	4
	LD	C,A		;REL.ADR.4
	CALL	KU.RSI		;RUECKSETZEN INTERN
	INC	C		;REL. ADR. 5
	LD	A,2
	OUT	A		;AUF=1
;-----------------------------------------------------------0250
	INC	C		;REL.ADR.6
	CALL	KU.AEBEI	;SIGNAL AEB AUF INTERRUPT
;-----------------------------------------------------------0260
	DEC	C
	DEC	C		;REL.ADR.4
	IN	A
	BIT	3,(IX+4)
	JRZ	STT17-#		;SPRUNG FALLS MIT RAW
	SET	6,A		;HGE=1
	LD	HL,KA.FEHLZ
	RES	0,M
;-----------------------------------------------------------0270
STT17:	SET	4,A		;TRV=1
	OUT	A		;EINSCHALTEN TRANSPORT VORWAERTS
;-----------------------------------------------------------0280
;AUFRUF CTC-ROUTINE
	LD	B,4		;ENTSPRICHT 40MS
	CALL	KU.AUFZV
	JRNC	4
	LD	B,7		;ENTSPRICHT 70 MS
	LD	C,0
	LD	HL,STT14
	CALL	KC.INIT		;ZEITSTEUERUNG
;-----------------------------------------------------------0290
STT13:	CALL	KU.RLAD
	POP	IX
	EI
	RET
;-----------------------------------------------------------0300
STT14:	DI			;EINSPRUNG NACH ABLAUF ZEITVORGABE
	PUSH	IX
	CALL	KU.RRET
	LD	IX,(KA.AEATB)
;-----------------------------------------------------------0310
	LD	A,(IX+2)
	ADD	3
	LD	C,A		;REL.ADR.3
	LD	L,L(KT.IER)
	BIT	3,(IX+4)
	CAZ	KU.EINIE	;INT.EINGABEKANAL EINSCHALTEN,RAW
;-----------------------------------------------------------0320
STT15:	DEC	C		;REL.ADR.2
	LD	A,83H
	OUT	A		;INT.FREIGABE AUSGABEKANAL
;-----------------------------------------------------------0330
	DEC	C
	DEC	C		;REL.ADR.0
	LD	A,0AAH
	OUT	A		;AUSGABE PRAEAMBEL
;-----------------------------------------------------------0340
	CALL	KU.STAE		;STATUS AUF INT.
	JR	STT13-#
;-----------------------------------------------------------0350

;EINGABEZWEIG
STT20:	BIT	1,(IX)		;DATENTRAEGERENDE
	JRNZ	STT21-#
;-----------------------------------------------------------0360
	IN	A
	BIT	7,A
	CAZ	KU.MBAN		;MB EINZIEHEN BEIM LESEN
;-----------------------------------------------------------0370
STT21:	XOR	A
	LD	(KA.Z1),A
;-----------------------------------------------------------0380
STT22:	LD	DE,0		;EINSPRUNG
	LD	(KA.ZEIN),DE	;ZEIN=0
	LD	HL,KA.PUFFS
	LD	(KA.PZEIN),HL	;ZEIGER AUF PUFFERANFANG
	XOR	A
	LD	(KA.FEHLZ),A
;-----------------------------------------------------------0390
	CALL	KU.RSI		;RUECKSETZEN INTERN AKB
	LD	L,L(KT.IEL)	;L-TEIL INT.-VEKTOR LESEN
	DEC	C		;REL. ADR.3
	CALL	KU.EINIE	;INT.EINGABEKANAL EINSCH. AUF LESEN
;-----------------------------------------------------------0400
	LD	A,(IX+2)
	ADD	6
	LD	C,A		;REL.ADR.6
	CALL	KU.AEBEI	;AEB AUF INT.
;-----------------------------------------------------------0410
	DEC	C
	DEC	C		;REL.ADR.4
	IN	A
	SET	4,A
	OUT	A		;TRV=1
;-----------------------------------------------------------0420
	EI
	XOR	A
	LD	(KA.CONTR),A
	LD	B,68
	CALL	KU.AUFZV
	JRNC	4
	LD	B,134
	LD	C,1		;KONTROLLE EREIGNIS
	LD	HL,STF05
	CALL	KC.INIT		;ZEITKONTROLLE
	JMP	STT13
;-----------------------------------------------------------0430

;RESERVIEREN EIN
STT30:	IN	A		;TOR A
	AND	3
	BIT	0,(IX+3)
	JRZ	STT31-#
	RES	1,A		;GERAET 1
	LD	D,10H
	JR	STT32-#
STT31:	RES	0,A		;GERAET 2
	LD	D,08H
STT32:	OUT	A		;RESERVIERUNG EIN
	INC	C		;REL.ADR.5
	LD	E,2
;-----------------------------------------------------------0440
STT33:	LD	A,150
	CALL	KU.WART
	DEC	E
	JRNZ	STT33-#
;-----------------------------------------------------------0450
	IN	A
	AND	D
	JRNZ	STT34-#
;-----------------------------------------------------------0460
STT37:	LD	(IX),10H	;GERAET NICHT BEREIT
	JMP	KU.KOMED
;-----------------------------------------------------------0470

;ANWAHL EIN,ZUSTAENDE ABFRAGEN
STT34:	DEC	C		;REL.ADR.4
	IN	A
	BIT	0,(IX+3)	;SUBADRESSE
	JRZ	STT35-#
	SET	3,A		;ANWAHL GERAET 1
	JR	4
STT35:	SET	2,A		;ANWAHL GERAET 2
	OUT	A
	INC	C		;REL.ADR.5
	LD	A,2
	NOP			;ED 79  OUT  A;AUFZEICHNEN EIN
	NOP
	IN	A
	BIT	0,A		;AUFZEICHNUNGSVERFAHREN
	JRZ	6
	SET	6,(IX)
	BIT	2,A		;KASSETTENSEITE
	JRZ	6
	SET	3,(IX)
	IN	A
	BIT	6,A		;AUFZEICHNEN VERBOTEN ?
	JRZ	6
	NOP			;DD CB 00 D6  SET  2,(IX)
	NOP
	NOP
	NOP
	XOR	A
	OUT	A		;AUF=0
	DEC	C		;REL.ADR.4
	IN	A
	AND	3
	OUT	A		;ANWAHL RUECKSETZEN
	INC	C		;REL. ADR. 5
	IN	A
	AND	D
	JRZ	STT37-#
	RES	4,(IX)		;NICHTBEREIT RUECKSETZEN
	SET	5,(IX)		;GERAET RESERVIERT
;-----------------------------------------------------------0480
	JMP	KU.KOMED
;-----------------------------------------------------------0490
;RESERVIEREN RUECKSETZEN
STT40:	IN	A
	BIT	0,(IX+3)	;SUBADRESSE
	JRZ	STT41-#
	SET	1,A
	JR	STT42-#
STT41:	SET	0,A
STT42:	AND	3
	OUT	A		;RES.RUECKSETZEN
	LD	(IX),10H	;STATUS NICHT BEREIT
;-----------------------------------------------------------0500
	JMP	KU.KOMED
;-----------------------------------------------------------0510

;UMSPULEN
STT50:	IN	D		;EINGABE VON REL. ADR. 4
	BIT	7,D
	JRNZ	STT52-#
;-----------------------------------------------------------0520
	SET	5,D
	OUT	D		;TRR=1
;-----------------------------------------------------------0521
	LD	HL,0
STT51:	DEC	HL
	LD	A,H
	OR	L
	JRZ	STT54-#
;-----------------------------------------------------------0523
	IN	D
	BIT	7,D
	JRZ	STT51-#		;SPRUNG WENN AEB=0
;-----------------------------------------------------------0530
STT52:	RES	5,D
	OUT	D		;TRR=0
	LD	A,40
	CALL	KU.WART
	LD	A,D
	OR	30H		;TRR=TRV=1
	OUT	A
	LD	A,20
	CALL	KU.WART
STT53:	CALL	KU.BERT		;BEREITSIGNAL ABFRAGEN
	JRNC	STT53-#		;SPRUNG SOLANGE GERAET BEREIIT
;-----------------------------------------------------------0550
STT54:	IN	A
	AND	3
	OUT	A		;TRANSPORT UND ANWAHL AUS
	LD	A,40
	CALL	KU.WART
;-----------------------------------------------------------0560
	RES	1,(IX)		;DATENTRAEGERENDE RUECKSETZEN
;-----------------------------------------------------------0570
	JMP	KU.KOMED

;-----------------------------------------------------------0580
;VOR-/RUECKSETZEN UM EINEN BLOCK
STT60:	LD	B,10H		;VORWAERTS
	BIT	2,(IX+4)	;VORWAERTS/RUECKWAERTS?
	JRZ	4
	LD	B,20H		;RUECKWAERTS
	CALL	KU.BLVR
;-----------------------------------------------------------0590
	JMP	KU.KOMED
;-----------------------------------------------------------0600
;SCHREIBEN EINER BANDMARKE
STT70:	BIT	1,(IX)		;DATENTRAEGERENDE?
	JRNZ	STT71-#
;-----------------------------------------------------------0610
	IN	D
	BIT	7,D
	CAZ	KU.MBAN		;MB EINZIEHEN BEIM SCHREIBEN
;-----------------------------------------------------------0620
STT71:	XOR	A
	LD	(KA.Z1),A
	LD	(KA.Z2),A
;-----------------------------------------------------------0630
STT72:	LD	A,3		;EINSPRUNG
	LD	(KA.FEHLZ),A	;FEHLERANZEIGER
	LD	DE,4
	LD	(KA.ZEIN),DE	;ZAEHLER EINGABE
	LD	(KA.ZAUS),DE	;ZAEHLER AUSGABE
	LD	HL,KA.PUFFS	;ADRESSE PUFFERSPEICHER
	LD	(KA.PZAUS),HL
	LD	(KA.PZEIN),HL
;-----------------------------------------------------------0640
	LD	M,0AAH		;LADEN PUFFERSPEICHER
	INC	HL
	LD	M,0
	INC	HL
	LD	M,0
	INC	HL
	LD	M,0AAH
;-----------------------------------------------------------0650
	JMP	STT16
;-----------------------------------------------------------0660
;SCHREIBEN EINER SCHLUSSLUECKE (> 400 MM>

STT80:	LD	B,108
	CALL	KU.MBVZ		;MB 400 MM LOESCHEN
;-----------------------------------------------------------0670
	DEC	C
	DEC	C		;REL.ADR.4
	IN	A
	AND	3
	OUT	A		;ANWAHL AUS
;-----------------------------------------------------------0680
	JMP	KU.KOMED
;-----------------------------------------------------------0690
;SUCHE BANDMARKE
;<C>=REL.ADR.4
;<IX>=ADRESSE E/A-TABELLE
STT90:	LD	A,(IX+10)	;ANZAHL BANDMARKEN,DIE ZU UEBERLESEN SIND
	LD	(KA.Z1),A
	BIT	2,(IX+4)
	JRNZ	STT98-#		;SPRUNG WENN SUCHEN RUECKWAERTS
;-----------------------------------------------------------0700
	BIT	1,(IX)		;DATENTRAEGERENDE ?
	JRNZ	STT98-#
;-----------------------------------------------------------0710
	IN	D
	BIT	7,D		;AEB ?
	CAZ	KU.MBAN		;MB EINZIEHEN
;-----------------------------------------------------------0720
STT98:	DI
	INC	C
	INC	C		;REL ADR. 6
	CALL	KU.AEBEI
;-----------------------------------------------------------0730
	EI
	DEC	C
	DEC	C		;REL. ADR. 4
	CALL	STT9E		;TRANSPORT EINSCHALTEN
	SET	6,A
	OUT	A		;HGE=1
;-----------------------------------------------------------0740
	LD	A,50
	CALL	KU.WART
	INC	C		;REL.ADR.5
	JR	STT9B-#
;-----------------------------------------------------------0750
STT93:	LD	DE,7100
;IN FOLGENDER SCHLEIFE WIRD BMS 170 MILLISEK. AUF 1 GETESTET
STT91:	EI
	DEC	DE
	LD	A,D
	OR	E
	JPZ	STT9F		;SPRUNG WENN AUFZEICHNUNGSENDE
	IN	B
	BIT	5,B
	JRZ	STT91-#
;-----------------------------------------------------------0760
; EINGANG BMS=1
;INTEGRATION DES SIGNALS BMS UEBER CA. 1 MS
	DI
	LD	D,0
	LD	B,73		;INTEGRATION UEBER CA. 1,6 MS
STT99:	IN	A
	BIT	5,A
	JRZ	STT92-#
	INC	D
	JR	STT92+2-#
STT92:	IN	A
	DJNZ	STT99-#
;-----------------------------------------------------------0770
;PRUEFEN OB NACH BANDMARKE EINE BLOCKLUECKE (MIND. 2 MM)
	LD	B,255		;PRUEFEN CA. 10 MM
STT9W:	IN	A
	BIT	5,A
;-----------------------------------------------------------0771
	JRNZ	STT9B-#
	DJNZ	STT9W-#
;-----------------------------------------------------------0780
	LD	A,D
	CMP	15
	JRNC	STT94-#		;SPRUNG WENN A >=15
	JR	STT93-#
;-----------------------------------------------------------0790
STT9B:	LD	B,0FFH		;NAECHSTE BLOCKLUECKE SUCHEN
	IN	A
	BIT	5,A
	JRNZ	STT9B-#
	DJNZ	STT9B+2-#
	JR	STT93-#
;-----------------------------------------------------------0800
;BANDMARKE ERKANNT
STT94:	LD	HL,KA.Z1
	DEC	M		;ZAEHLER FUER BANDMARKEN
;-----------------------------------------------------------0801
	JRNZ	STT93-#
;-----------------------------------------------------------0810
	DEC	C		;REL. ADR. 4
	IN	A
	AND	4FH
	OUT	A		;TRANSPORT AUS
;-----------------------------------------------------------0820
	LD	B,50		;WARTEN 500 MS
	LD	C,0
	LD	HL,STT97
	CALL	KC.INIT		;NACH 250 MS STOP
STT9C:	HALT
	JR	STT9C-#
STT97:	INC	SP
	INC	SP
;-----------------------------------------------------------0830
	LD	B,100
	LD	C,0
	LD	HL,STF08
	CALL	KC.INIT		;TIME OUT WENN BANDMARKE NICHT GEFUNDEN
;-----------------------------------------------------------0840
	LD	A,(IX+2)
	ADD	4
	LD	C,A		;REL. ADR. 4
	IN	A
	SET	6,A		;HGE=1
	OUT	A
;-----------------------------------------------------------0850
	RES	6,A		;HGE=0
	BIT	2,(IX+4)	;VORW./RUECKW. ?
	JRZ	6
	SET	4,A
	JR	4
	SET	5,A
	OUT	A		;TRANSPORT EIN
;-----------------------------------------------------------0860
	LD	A,20
	CALL	KU.WART
	INC	C		;REL. ADR. 5
STT95:	EI
	IN	A
	BIT	5,A
	JRNZ	STT95-#
STT96:	EI
	IN	A
	BIT	5,A
	JRZ	STT96-#
;-----------------------------------------------------------0880
;BMS =1 FUER CA. 2200 MIKROSEK. ?
	DI
	LD	B,80
	CALL	KU.AUFZV
	JRNC	4
	LD	B,160
STT9X:	IN	A
	BIT	5,A
	JRZ	STT96-#		;SPRUNG WENN STOERUNG
	BIT	0,(IX)
	DJNZ	STT9X-#
;WIRD BMS=0 IN DEN NAECHSTEN 610 MIKROSEK. ?
	LD	B,40
	CALL	KU.AUFZV
	JRNC	4
	LD	B,80
STT9Y:	IN	A
	BIT	5,A
	JRZ	STT9A-#		;BANDMARKE
	BIT	0,(IX)
	DJNZ	STT9Y-#
	JR	STT95-#		;DATENBLOCK
;-----------------------------------------------------------0900
STT9A:	EI
	CALL	KC.SCTC		;CTC STOPPEN
;-----------------------------------------------------------0910
	CALL	KU.BLVR1-1	;IN BLOCKLUECKE FAHREN
	LD	A,50
	CALL	KU.WART
;-----------------------------------------------------------0920
	BIT	2,(IX+4)
	JRZ	7		;SPRUNG WENN SUCHEN VORWAERTS
;-----------------------------------------------------------0930
	LD	B,20H
	CALL	KU.BLVR		;1 BLOCK RUECKSETZEN
;-----------------------------------------------------------0940
	JMP	STT20		;LESEN
;-----------------------------------------------------------0950
;FEHLER:ENDE DER AUFZEICHNUNGEN
STT9F:	SET	7,(IX)
	LD	(IX+1),16H	;ENDE DER AUFZEICHNUNGEN
	DEC	C		;REL.ADR.4
	IN	A
	RES	6,A
	OUT	A		;HGE=0
	XOR	A		;A=0
	CALL	KU.WART		;256 MS WARTEN
	CALL	KU.AUS		;TRANSPORT UND ANWAHL AUS
	JMP	KU.KOMED
;-----------------------------------------------------------0960
STT9E:	IN	A		;TRANSPORTSIGNAL SETZEN
	BIT	2,(IX+4)
	JRNZ	STT9H-#
	SET	4,A
STT9G:	RET
STT9H:	SET	5,A
	JR	STT9G-#
;-----------------------------------------------------------0970

;FEHLER
STF01:	LD	L,12H		;SUBADRESSE 1,2
STF99:	LD	(IX+1),L	;FEHLERSCHLUESSEL LADEN
	SET	7,(IX)		;FEHLERBIT SETZEN
	CALL	KU.AUS		;ANWAHL UND SONST. SIGNALE AUS
STFRS:	JMP	KU.KOMED	;KOMMANDOENDE
STF02:	LD	L,11H		;GERAET NICHT RESERVIERT
	JR	STF99-#
STF03:	SET	4,(IX)		;GERAET NICHT BEREIT
	JR	STFRS-#
STF04:	LD	L,13H		;BLOCKLAENGE FALSCH
	JR	STF99-#
STF05:	PUSH	IX		;ENDE DER AUFZEICHNUNG (LESEN)
	CALL	KU.RRET
	LD	IX,(KA.AEATB)
STF9A:	LD	L,14H		;FEHLERSCHLUESSEL
	JR	STF99-#
STF06:	LD	L,15H		;GERAET BESETZT
	JR	STF99-#
STF07:	LD	L,10H		;FALSCHES KOMMANDO
	JR	STF99-#
STF08:	POP	DE
	LD	L,18H		;BANDMAKRE NICHT GEFUNDEN
	JR	STF99-#
STF0B:	LD	L,19H		;AUFZEICHNEN VERBOTEN
	JR	STF99-#
	END	

PN KI
	TITL	'KMBG-INTERRUPTSERVICEROUTINEN'
;************************************************************
;************************************************************
;**               **                                       **
;**    BAMOS      **   KASSETTENMAGNETBAND                 **
;**               **                                       **
;************************************************************
;** STAND:  01.01.81/19.07.84  OS-VERSION:   V1.2          **
;** BEARBEITER: HILDEBRANDT/ AENDERUNG: TOCHATSCHEK        **
;************************************************************
;** INTERRUPTSERVICEROUTINEN KMBG                          **
;************************************************************
;************************************************************

;-----------------------------------------------------------0010
;INTERRUPTBEHANDLUNG AUSGABEKANAL
IAUS:	PUSH	IX
	CALL	KU.RRET
	LD	IX,(KA.AEATB)
;-----------------------------------------------------------0020
	LD	C,(IX+2)	;BASISADRESSE AKB
	LD	HL,(KA.PZAUS)
	LD	DE,(KA.ZAUS)
	DEC	DE
	LD	A,D
;-----------------------------------------------------------0021
	OR	E
	JRZ	IAUSE-#		;SPRUNG WENN AUSG. BEENDET
;-----------------------------------------------------------0030
	INC	HL
	LD	A,M
	OUT	A		;AUSGABE ZEICHEN
;-----------------------------------------------------------0040
	LD	(KA.PZAUS),HL
	LD	(KA.ZAUS),DE
	JMP	KU.ENDI
;-----------------------------------------------------------0050
IAUSE:	LD	B,3
	INC	C
	INC	C		;REL.ADR.2
	OUT	B		;INT.AUSGABEKANAL SP.
;-----------------------------------------------------------0060
	LD	A,C
	ADD	5
	LD	C,A		;REL.ADR.7
	OUT	B		;INT.STATUS SPERREN
	CALL	KU.ENDI1	;INT. FREIGABE DASY-CHAIN
	EI
;-----------------------------------------------------------0070
	LD	C,0
	LD	B,3		;30 MS
	CALL	KU.AUFZV
	JRNC	4
	LD	B,6		;60 MS
IAUS0:	LD	HL,IAUSS	;ZEITSTEUERUNG
	CALL	KC.INIT
	JMP	KS.STT13
;-----------------------------------------------------------0080
IAUSS:	DI
	PUSH	IX
	CALL	KU.RRET
	LD	IX,(KA.AEATB)
	LD	A,(IX+2)
	ADD	4
	LD	C,A		;REL.ADR.4
	IN	A		;REL.ADR.4
	BIT	4,A
	JRZ	IAUS1-#
	AND	0FH		;TRV=0
	OUT	A
;-----------------------------------------------------------0090
	LD	C,0
	LD	B,7
	JR	IAUS0-#		;70 MS WARTEN
;-----------------------------------------------------------0100
IAUS1:	INC	C		;REL.ADR.5
	XOR	A
	OUT	A		;AUF=0
	INC	C		;REL.ADR.6
	LD	A,3
	OUT	A
	DEC	C		;AEB-INT.GESPERRT
;-----------------------------------------------------------0110
	DEC	C		;REL.ADR.4
	DEC	C		;REL. ADR. 3
	LD	HL,KP.INI1B	;EINGK. INT. SPERREN
	LD	B,3
	OTIR
;-----------------------------------------------------------0120
	INC	C		;REL. ADR. 4
	LD	A,(KA.FEHLZ)
	BIT	3,(IX+4)	;MIT RAW  ?
	JRZ	IAUS2-#		;SPRUNG WENN OHNE RAW
	BIT	2,A		;STATUS WAEHREND DER UEBERTRAGUNG ?
	JR	IAUS3-#
IAUS2:	OR	A
IAUS3:	JPNZ	KF.FEHLB	;FEHLERBEHANDLUNG
;-----------------------------------------------------------0130
	IN	A
	AND	3
	OUT	A		;AWA=0
	LD	A,(KA.Z1)
	LD	(IX+11),A
	LD	A,(KA.Z2)
	LD	(IX+12),A
	JMP	KU.KOMED
;-----------------------------------------------------------0140

;INTERRUPTBEHANDLUNG EINGABEKANAL RAW
IER:	PUSH	IX
	CALL	KU.RRET
	LD	IX,(KA.AEATB)	;ADRESSE E/A-TAB.
;-----------------------------------------------------------0150
	LD	C,(IX+2)
	INC	C		;REL.ADR.1
	IN	A		;EINGABE ZEICHEN
;-----------------------------------------------------------0160
	LD	HL,KA.FEHLZ
	LD	D,M		;<D>=FEHLZ
	RES	0,D		;EINGABEKANAL ANGESPROCHEN
	LD	HL,(KA.PZEIN)
	CMP	M
	JRZ	IER0-#
;-----------------------------------------------------------0170
	BIT	4,D
	CAZ	IERF
;-----------------------------------------------------------0180
IER0:	INC	HL
	LD	(KA.PZEIN),HL
	LD	HL,(KA.ZEIN)	;PUFFERZEIGER ERHOEHT
	DEC	HL
;-----------------------------------------------------------0190
	LD	(KA.ZEIN),HL	;ZAEHLER ERNIEDRIGT
	LD	A,H
	OR	L
	JRZ	IERE-#		;SPRUNG ZUR ENDEBEH.
;-----------------------------------------------------------0200
IER1:	LD	A,D
	LD	(KA.FEHLZ),A
	JMP	KU.ENDI
;-----------------------------------------------------------0210
IERE:	RES	1,D
	JR	IER1-#
;-----------------------------------------------------------0220
IERF:	LD	(KA.IDAT),A	;IST-DATEN
	LD	A,M
	LD	(KA.SDAT),A	;SOLL-DATEN
	LD	BC,KA.PUFFS	;ADRESSE PUFFERSPEICHER
	XOR	A		;CY=0
	PUSH	HL
	SBC	HL,BC		;RELATIVE POSITION
	LD	(KA.POS),HL
	POP	HL
	SET	4,D		;FEHLERBIT:RAW-FEHLER
	RET
;-----------------------------------------------------------0230

;INTERRUPTBEHANDLUNG EINGABEKANAL
IEL:	PUSH	IX
	CALL	KU.RRET
	LD	IX,(KA.AEATB)
;-----------------------------------------------------------0240
	CALL	KC.SCTC		;CTC SPERREN
;-----------------------------------------------------------0250
	LD	C,(IX+2)
	INC	C		;REL.ADR.1
	IN	A		;EINGABE ZEICHEN
;-----------------------------------------------------------0260
	CMP	0AAH
	JRNZ	IEL01-#
;-----------------------------------------------------------0270
	LD	B,72
IEL00:	DJNZ	IEL00-#		;WARTEN 370 MIKROS.
;-----------------------------------------------------------0280
	INC	C
	INC	C
	INC	C
	INC	C		;REL.ADR.5
	IN	D
	BIT	5,D		;BMS=1?
	JRZ	IEL02-#		;SPRUNG ZUR ENDEBEH.
;-----------------------------------------------------------0290
IEL01:	LD	HL,(KA.PZEIN)
	LD	M,A		;ABSPEICHERN ZEICHEN
;-----------------------------------------------------------0300
	INC	HL
	LD	(KA.PZEIN),HL	;PUFFERZEIGER ERHOEHT
;-----------------------------------------------------------0310
	LD	DE,KA.KOMDO
	XOR	A
	SBC	HL,DE
	JPZ	IELFF		;SPRUNG WENN PUFFERSP. UEBERSCHR.
;-----------------------------------------------------------0320
	LD	HL,(KA.ZEIN)
	INC	HL
	LD	(KA.ZEIN),HL
;-----------------------------------------------------------0330
	JMP	KU.ENDI
;-----------------------------------------------------------0340
IEL02:	DEC	C
	DEC	C		;REL.ADR.3
	LD	A,3
	OUT	A		;INT.EINGABEKANAL GESP.
	LD	HL,KA.CONTR
	LD	M,1
;-----------------------------------------------------------0350
	CALL	KU.ENDI1	;RETI
	EI
;-----------------------------------------------------------0360
	LD	HL,(KA.PZEIN)	;CRC-ZEICHEN ABSPEICHERN
	DEC	HL
	LD	A,M
	LD	(KA.CRCZ+1),A	;2.BYTE CRC
	DEC	HL
	LD	A,M
	LD	(KA.CRCZ),A	;1. BYTE CRC-ZEICHEN
;-----------------------------------------------------------0370
	LD	B,2
	LD	C,0
	CALL	KU.AUFZV
	JRNC	4
	LD	B,3
IEL06:	LD	HL,IEL03
	CALL	KC.INIT
	JMP	KS.STT13
IEL03:	PUSH	IX
	CALL	KU.RRET
	LD	IX,(KA.AEATB)
;-----------------------------------------------------------0380
	LD	A,(IX+2)
	ADD	4
	LD	C,A
	IN	A
	BIT	4,A		;TRV=0 ?
	JRZ	IEL07-#
	AND	0FH
	OUT	A
;-----------------------------------------------------------0390
	LD	C,0
	LD	B,7
	JR	IEL06-#		;WARTEN 70 MS
;-----------------------------------------------------------0400
IEL07:	LD	HL,(KA.ZEIN)	;BLOCKLAENGE-1
	LD	DE,3
	XOR	A
	SBC	HL,DE
	JRZ	IEL05-#		;SPRUNG WENN STEUERBLOCK
;-----------------------------------------------------------0410
	PUSH	HL
	POP	BC
	LD	(IX+9),B
	LD	(IX+10),C
	LD	HL,KA.PUFFS+1	;ADRESSE DATEN
	LD	(IX+7),L
	LD	(IX+8),H
;-----------------------------------------------------------0420
	LD	B,C
	CALL	KU.CRC		;CRC-ZEICHEN BERECHNEN
	INC	HL		;ADRESSE 1.BYTE CRC-ZEICHEN
	LD	B,M		;1.BYTE CRC-ZEICHEN NACH B
	INC	HL		;ADRESSE 2.BYTE CRC-ZEICHEN
	LD	C,M		;2.BYTE GELADEN NACH C
	PUSH	DE
	POP	HL
	XOR	A
	SBC	HL,BC		;DIFFERENZ DER CRC-ZEICHEN
;-----------------------------------------------------------0430
	CANZ	IELF		;FEHLER
;-----------------------------------------------------------0431
IEL04:	LD	A,(IX+2)
	ADD	4
	LD	C,A		;REL. ADR. 4
	LD	A,(KA.FEHLZ)
	OR	A
	JPNZ	KF.FEHLB
;-----------------------------------------------------------0440
	CALL	KU.AUS
	JMP	KU.KOMED
;-----------------------------------------------------------0450
IEL05:	XOR	A		;BANDMARKE GELESEN
	LD	(IX+7),A
	LD	(IX+8),A
	LD	(IX+9),A
	LD	(IX+10),A
	JR	IEL04-#
;-----------------------------------------------------------0460
IELF:	LD	HL,KA.FEHLZ
	SET	3,M
	RET
;-----------------------------------------------------------0470
IELFF:	CALL	KU.AUS
	CALL	KU.ENDI1	;RETI
	SET	7,(IX)
	LD	(IX+1),17H
	JR	IEL04-#

;INTERRUPTBEHANDLUNG STATUS
IST:	PUSH	IX
	CALL	KU.RRET
	LD	IX,(KA.AEATB)
	LD	A,(IX+2)
	ADD	7
	LD	C,A		;REL.ADR.7
	LD	HL,KA.FEHLZ	;INTERNER FEHLERZEIGER
	SET	2,M		;STATUSFEHLER
	LD	A,3
	OUT	A		;INT.STATUS SPERREN
	JMP	KU.ENDI

;INTERRUPTBEHANDLUNG AEB
IBE:	PUSH	AF
	PUSH	BC
	PUSH	IX
	LD	IX,(KA.AEATB)
	LD	A,(IX+2)
	ADD	6
	LD	C,A
	LD	A,03H
	OUT	A
	SET	1,(IX)
	POP	IX
	POP	BC
	POP	AF
	EI
IBE0:	RETI
	END	

PN KU

	TITL	'KMBG-UNTERPROGRAMME'

;************************************************************
;************************************************************
;**               **                                       **
;**   BAMOS       **   KASSETTENMAGNETBAND                 **
;**               **                                       **
;************************************************************
;** STAND:  01.01.81/19.07.84  OS-VERSION:   V1.2          **
;** BEARBEITER: HILDEBRANDT/ AENDERUNG: TOCHATSCHEK        **
;************************************************************
;** UNTERPROGRAMME ZUR BEDIENROUTINE KMBG                   **
;************************************************************
;************************************************************


;ENDEBEHANDLUNG FUER NORMALE INTERRUPTBEHANDLUNG
ENDI:	CALL	RLAD
	POP	IX
	EI
ENDI1:	RETI

;VERZWEIGUNG AUS STEUERTEIL ZUM EINTRITTS-
;PUNKT DES STEUERPROGRAMMES
KOMED:	DI
	RES	0,(IX)
	LD	L,(IX+5)
	LD	H,(IX+6)
	PUSH	HL
	POP	IX
	CALL	RLAD
	EX	(SP),IX
	EI
	RET

;UP ZUR ABFRAGE DES BEREITSIGNALS AN DER PIO
;UP STELLT CARRY-FLAG, C=0 GERAET BEREIT
;                      C=1 GERAET NICHT BEREIT
;<IX>=ANFANGSADRESSE E/A-TABELLE
;UP ZERSTOERT REG. AF

BERT:	PUSH	BC
	LD	A,(IX+2)
	ADD	5
	LD	C,A
	IN	A
	SCF
	BIT	0,(IX+3)
	JRZ	BERT2-#
	BIT	4,A
	JRZ	BERTE-#
	CCF
	JR	BERTE-#
BERT2:	BIT	3,A
	JRZ	BERTE-#
	CCF
BERTE:	POP	BC
	RET

;UP ZUR BILDUNG VON RSI
;<C>=REL.ADR.4
RSI:	IN	B
	LD	A,B
	AND	73H		;EAW1=EAW2=0
	OUT	A
	OUT	B
	RET

;REGISTER AF,BC,DE,HL RETTEN IN SP
RRET:	EX	(SP),HL
	PUSH	DE
	PUSH	BC
	PUSH	AF
	JMP	(HL)

;REGISTER AF,BC,DE,HL LADEN AUSSP
RLAD:	EX	(SP),HL
	INC	SP
	INC	SP
	POP	AF
	POP	BC
	POP	DE
	EX	(SP),HL
	RET

;UP ZUM TESTEN BMS AUF STOERUNG
;<C>=REL.ADR. 5
; BMS=1 BEI AURUF DES UP
;REG. A,B,F WERDEN ZERSTOERT
;AUSGANG: CY=1 STOERUNG
;         CY=0 BMS=1 GROESSER 0,2 MM BAND
BMST:	LD	B,43
	CALL	AUFZV
	JRNC	4
	LD	B,73
BMST0:	SCF
	IN	A
	BIT	5,A
	RZ
	DJNZ	BMST0-#
	CCF
	RET

;VOR-/RUECKSETZEN UM EINEN BLOCK
;<C>=REL.ADR.4
;<B>=10H    VORWAERTS
;<B>=20H    RUECKWAERTS
;REG. A,B   WERDEN ZERSTOERT
BLVR:	DI
	IN	A
	SET	6,A		;HGE=1
	OUT	A
	RES	6,A		;HGE=0
	OR	B
	OUT	A		;TRANSPORT EINSCHALTEN
	INC	C		;REL.ADR.5
	LD	A,18
	CALL	WART		;WARTEN BIS NENNGESCHW.
BLVR0:	IN	A		;BMS?
	BIT	5,A
	JRZ	BLVR0-#
	CALL	BMST		;TEST BMS AUF STOERUNG
	JRC	BLVR0-#		;SPRUNG WENN STOERUNG
	PUSH	DE
BLVR1:	IN	A
	BIT	5,A
	JRNZ	BLVR1-#		;SPRUNG SOLANGE BMS=1
;TEST AUF WEITERE EINBRUECHE DES SIGNALS BMS
	LD	DE,1243
	CALL	AUFZV
	JRNC	5
	LD	DE,2483
BLVR2:	IN	A
	BIT	5,A
	JRNZ	BLVR3-#
	DEC	DE
	LD	A,D
	OR	E
	JRNZ	BLVR2-#
	DEC	C
	IN	A
	AND	0FH
	OUT	A		;TRV=TRR=0
	POP	DE
	EI
	RET
BLVR3:	CALL	BMST
	JRNC	BLVR1-#		;KEINE STOERUNG
	JR	BLVR2-#		;STOERUNG



;CRC-ZEICHEN BERECHNEN
;<B>=BLOCKLAENGE,0=256
;<HL>=PUFFERADRESSE
;IN DE NACH AUFRUF CRC-ZEICHEN
; <D>=1.BYTE, WIRD ZUERST GESENDET
; <E>=2. BYTE,WIRD DANACH GESENDET

CRC:	LD	DE,0
CRC0:	PUSH	BC		;BYTELAENGE
	LD	B,8
	LD	C,M
CRC1:	LD	A,C
	XOR	D
	RRCA			;ERGEBNIS ANTIVALENZ WORT-BYTE/CRC-BYTE,BIT 0
	PUSH	AF
	RR	E		;VERSCHIEBUNG VON DE UM 1 BIT NACH RECHTS
	RR	D
	POP	AF		;RUECKRETTEN F
	JRNC	CRC2-#		;SPRUNG WENN ANTIVALENZ 0+0=0
	LD	A,D		;KORREKTUR=UMKEHR BIT13/BIT0
	XOR	1		;MASKE FUER D
	LD	D,A		;KORREKTUR D
	LD	A,E
	XOR	20H		;MASKE FUER E
	LD	E,A		;KORREKTUR E
CRC2:	RRC	C		;SCHIEBEN DATENBYTE
	DJNZ	CRC1-#
	POP	BC		;BYTE ABGEARBEITET
	DJNZ	CRC3-#		;ZAHLER FUER BLOCKLAENGE
	RET
CRC3:	INC	HL		;ADRESSE NAECHSTES DATENBYTE AUS PUFFER
	JR	CRC0-#


;UP ZUM EINSCHALTEN DES AEB-INT.
;<C>=REL.ADR.6
;<A>=WIRD ZERSTOERT
AEBEI:	LD	A,L(KT.IBE0)
	OUT	A
	LD	A,83H
	OUT	A
	EI
	NOP
	NOP
	DI
	LD	A,L(KT.IBE)
	OUT	A
	RET

;UP ZUM EINSCHALTEN DES STATUSINTERRUPTS
;<IX>=ANFANGSADRESSE E/A-TABELLE
STAE:	DI
	LD	A,(IX+2)
	ADD	7
	LD	C,A		;REL.ADR.7
	LD	A,L(KT.IBE0)
	OUT	A
	LD	A,83H
	OUT	A		;FREIGABE BLINDINTERRUPT
	EI
	NOP
	NOP
	DI
	LD	HL,KP.INI2B
	LD	B,5
	OTIR
	OUT	A		;FREIGABE INT.STATUS
	RET

;WARTEN, <A>=ANZAHL MILLISEKUNDEN > 0
;REGISTER A,B WERDEN ZERSTOERT
WART:	LD	B,188
	DJNZ	0
	DEC	A
	JRNZ	WART-#
	RET

;UP ZUM EINSCHALTEN INTERRUPT EINGABEKANAL
;EINGANG: <L>=L-TEIL INT.-ADRESSE
;         <C>=REL.ADR.3
;REG.B WIRD ZERSTOERT
EINIE:	LD	A,L(KT.IBE0)	;ADRESSE RETI-BEFEHL
	OUT	A
	LD	A,83H		;INT.FREIGABE
	OUT	A
	EI
	NOP
	NOP
	DI
	OUT	L		;INT.-VEKTOR RAW
	RES	1,C		;REL.ADR.1
	IN	B
	SET	1,C		;REL. ADR. 3
	RET			;REDY EINSCHALTEN
;MAGNETBAND AUS ANFANGSSTELLUNG EINZIEHEN
;<IX>=ANFANGSADRESSE E/A-TABELLE
;<C>=REL.ADR.4
MBAN:	INC	C		;REL.ADR.5
	LD	A,(IX+4)
	AND	7FH		;BIT 7 AUSBLENDEN
	CMP	2		;LESEN ?
	JRZ	MBAN0-#		;AUF=0
	CMP	71H		;BM SUCHEN VORWAERTS ?
	JRZ	MBAN0-#		;AUF=0
	LD	B,2
	OUT	B		;AUF=1
MBAN0:	DEC	C		;REL.ADR.4
	IN	B
	SET	4,B
	OUT	B		;TRV=1
MBAN1:	IN	B
	BIT	7,B		;AEB?
	JRZ	MBAN1-#
	LD	B,255
	DJNZ	0		;WARTEN 1,3 MS
	IN	B
	BIT	7,B
	JRZ	MBAN1-#		;SPRUNG WENN STOERUNG
MBAN2:	IN	B
	BIT	7,B
	JRNZ	MBAN2-#		;SPRUNG SOLANGE AEB=1
	BIT	1,A
	JRNZ	MBAN5-#
	CMP	71H
	JRZ	MBAN5-#
	LD	HL,MBAN6
	LD	B,40
	CALL	KU.AUFZV
	JRNC	4
	LD	B,80
MBAN3:	PUSH	BC
	LD	C,0
	CALL	KC.INIT		;150 MM INITIALLUECKE
MBAN4:	HALT
	JR	MBAN4-#
MBAN5:	LD	B,1
	LD	HL,MBAN6
	JR	MBAN3-#		;10 MS WARTEN
MBAN6:	POP	BC
	POP	BC
	IN	A
	RES	4,A
	OUT	A		;TRV=0
	LD	B,4
	LD	HL,MBAN7	;40 MS WARTEN
	JR	MBAN3-#
MBAN7:	POP	BC
	POP	BC
	INC	C		;REL. ADR. 5
	XOR	A
	OUT	A		;AUF=0
	DEC	C		;REL. ADR.4
	RET

;LOESCHE BAND VON 6 ODER 40 CM LAENGE
;<IX>=ANFANGSADRESSE E/A-TABELLE
;<B>=ZEITKONSTANTE FUER 38 CM*S**-1
;<C>=REL.ADR.6 BEIM AUSGANG
MBVZ:	LD	A,(IX+2)	;BASISADRESSE
	ADD	5
	LD	C,A		;REL.ADR.5
	LD	A,2
	OUT	A		;AUF=1
	INC	C		;REL.ADR.6
	CALL	AEBEI		;AEB.AUF INTERRUPT
	DEC	C
	DEC	C		;REL.ADR.4
	IN	A
	SET	4,A
	OUT	A		;TRV=1
	CALL	KU.AUFZV
	JRNC	MBVZ0-#
	SLA	B		;MULTIPLIKATION MIT 2
MBVZ0:	LD	HL,MBVZ3
MBVZ1:	PUSH	BC
	LD	C,0
	CALL	KC.INIT
MBVZ2:	HALT
	JR	MBVZ2-#
MBVZ3:	POP	BC
	POP	BC		;REL.ADR.4
	IN	A
	RES	4,A
	OUT	A		;TRV=0
	LD	B,4		;40 MS WARTEN
	LD	HL,MBVZ4
	JR	MBVZ1-#
MBVZ4:	POP	BC
	POP	BC
	INC	C		;REL.ADR.5
	XOR	A
	OUT	A		;AUF=0
	INC	C		;REL.ADR.6
	LD	A,3
	OUT	A		;AEB FUER INT.GESPERRT
	RET

;UP ZUM AUSSCHALTEN TRANSPORT,ANWAHL,AEB-INT.,STAT.-INT.,INT. E/A-KANAL
;<IX>=ANFANGSADRESSE E/A-TABELLE;REG. AF,BC,DE WERDEN ZERSTOERT
AUS:	LD	C,(IX+2)	;BASISADRESSE
	INC	C
	INC	C		;REL. ADR. 2
	LD	D,3
	OUT	D		;AUSGABEKANAL GESPERRT
	INC	C		;REL ADR. 3
	OUT	D		;EINGABEKANAL GESPERRT
	INC	C		;REL. ADR. 4
	IN	A
	AND	0FH
	OUT	A		;TRANSPORT AUS
	LD	A,40
	CALL	WART		;40 MS WARTEN
	IN	A
	AND	3
	OUT	A		;ANWAHL AUS
	INC	C		;REL. ADR. 5
	LD	E,0
	OUT	E		;AUF=0
	INC	C		;REL. ADR. 6
	OUT	D		;AEB-INT. GESPERRT
	INC	C		;REL. ADR. 7
	OUT	D		;STAT.-INT. GESPERRT
	RET

;UP ZUR ABFRAGE AZV
;CY=1 WENN GESCHW.=19 CM*S**-1
;CY=0 WENN GESCHW.=38 CM*S**-1
;<IX>=ADRESSE E/A-TABELLE
;AF WIRD ZERSTOERT
AUFZV:	PUSH	BC
	LD	A,(IX+2)
	ADD	5
	LD	C,A
	SCF
	IN	B
	BIT	0,B
	JRNZ	3
	CCF
	POP	BC
	RET
	END	

PN KC

	TITL	'KMBG-CTC ROUTINE'

;************************************************************
;************************************************************
;**               **                                       **
;**   BAMOS       **   KASSETTENMAGNETBAND                 **
;**               **                                       **
;************************************************************
;** STAND:  01.01.81/19.07.84  OS-VERSION:   V1.2          **
;** BEARBEITER: HILDEBRANDT/ AENDERUNG: TOCHATSCHEK        **
;************************************************************
;** ROUTINE ZUR CTC BEDIENUG                               **
;************************************************************
;************************************************************

;ZEITKONTROLLROUTINE FUER KASSETTENMAGNETBAND
;BEIM AUFRUF DES INITIALISIERUNGSTEILS:
;(C)=0 SPRUNG ZUR ADRESSE,KOMMANDO
;(C)=1 KONTROLLE EREIGNIS
;(B)=ZEITKONSTANTE
;(HL)=ADRESSE
INIT:	DI
	LD	(KA.KOMDO),BC	;ARBEITSBEREICH LADEN
	LD	(KA.ADR),HL
	LD	HL,INITK	;KANAL X AUF 10 MS INTERRUPTFREQUENZ PROGR.
	LD	C,L(KK.CTCBA)
	LD	B,1
	OTIR
	CALL	LDADR
	LD	B,2
	OTIR
	EI
	RET
;
SCTC:	PUSH	BC		;CTC KANAL X INTERRUPT SPERREN
	CALL	LDADR
	LD	A,3
	OUT	A
	POP	BC
	RET
;
;INTERRUPTSERVICEROUTINE CTC KANAL X
ICTC:	PUSH	AF
	PUSH	HL
	LD	HL,KA.ZEITK
	DEC	M
	POP	HL
	JRZ	ICTCE-#
ICTCR:	POP	AF
	EI
	RETI
ICTCE:	CALL	SCTC
	LD	A,(KA.KOMDO)
	OR	A
	JRNZ	ICTCK-#
ICTCS:	POP	AF
	PUSH	HL
	LD	HL,(KA.ADR)
	EX	(SP),HL
	EI
	RETI
ICTCK:	LD	A,(KA.CONTR)
	OR	A
	JRZ	ICTCS-#
	XOR	A
	LD	(KA.CONTR),A
	JR	ICTCR-#
;UP ZUM LADEN VON REG.C MIT ADRESSE CTC-KANAL
LDADR:	LD	A,L(KK.CTCBA)	;BASISADR. CTC
	ADD	L(KK.CTCKA)	;CTC-KANAL
	LD	C,A
	RET
;*** KONSTANTEN ZUM INIT.CTC ***
INITK:	DB	L(KV.ICTC0)	;ADRESSE
	DB	0A5H		;KANALSTEUERWORT
	DB	96		;ZEITKONSTANTE
;******************************
	END	

PN KF

	TITL	'KMBG-FEHLERBEHANDLUNG'

;************************************************************
;**               **                                       **
;**   BAMOS       **   KASSETTENMAGNETBAND                 **
;**               **                                       **
;************************************************************
;** STAND:  01.01.81/19.07.84  OS-VERSION:   V1.2          **
;** BEARBEITER: HILDEBRANDT/ AENDERUNG: TOCHATSCHEK        **
;************************************************************
;** FEHLERBEHANDLUNG ZUR BEDIENROUTINE KMBG                **
;************************************************************
;************************************************************

;-----------------------------------------------------------0010
FEHLB:	LD	B,(IX+4)
	BIT	7,B
	JRNZ	FEHL7-#		;OHNE FEHLERWIEDERHOLUNG
	BIT	1,B
	LD	B,20H
	LD	A,(KA.Z1)
	JRZ	FEHL2-#		;SPRUNG WENN AUSGABE
;-----------------------------------------------------------0020
	CMP	7
	JRZ	FEHL1-#		;SPRUNG WENN 7 X GELESEN
;-----------------------------------------------------------0030
	INC	A
	LD	(KA.Z1),A	;ZAEHLER ERHOEHEN
	CALL	KU.BLVR		;EINEN BLOCK RUECKSETZEN
;-----------------------------------------------------------0050
	LD	A,50
	CALL	KU.WART
	JMP	KS.STT22	;LESEN WIEDERHOLEN
;-----------------------------------------------------------0060
FEHL7:	BIT	1,B		;OHNE FEHLERWIEDERHOLUNG
FEHL4:	LD	A,22H
	JRZ	FEHL8-#		;SCHREIBEN
FEHL1:	LD	A,21H		;FEHLER NACH 7X LESEN
FEHL8:	LD	(IX+1),A	;EINTRAGEN FEHLERNUMMER
FEHLE:	SET	7,(IX)		;FEHLERBIT SETZEN
	JMP	KU.KOMED
;
FEHL6:	LD	A,23H		;KEIN ECHOSIGNAL VOM EINGABEKANAL BEI RAW
	JR	FEHL8-#
;
FEHL2:	LD	HL,KA.FEHLZ	;FEHLERANZEIGER
	BIT	0,M
	JRNZ	FEHL6-#
;-----------------------------------------------------------0090
	CMP	7
	JRZ	FEHL3-#		;SPRUNG WENN 7X GESCHRIEBEN
	INC	A
	LD	(KA.Z1),A	;ZAEHLER
	CALL	KU.BLVR		;EINEN BLOCK RUECKSETZEN
;------------------------------------------------------------0120
	LD	A,70
	CALL	KU.WART
;------------------------------------------------------------------0130
FEHL5:	LD	A,(IX+4)
	CMP	51H
	JPZ	KS.STT72	;SCHREIBEN BANDMARKE
;---------------------------------------------------------0140
	JMP	KS.STTEP	;SCHREIBWIEDERHOLUNG
;-----------------------------------------------------------0150
FEHL3:	LD	A,(KA.Z2)
	CMP	2		;BLOCKLUECKE 2 X VERLAENGERT ?
	JRZ	FEHL4-#
;-----------------------------------------------------------0160
	INC	A
	LD	(KA.Z2),A
;-----------------------------------------------------------0170
	CALL	KU.BLVR		;EINEN BLOCK RUECKSETZEN
;-----------------------------------------------------------0180
	LD	A,70
	CALL	KU.WART
;-----------------------------------------------------------0190
	LD	B,16
	CALL	KU.MBVZ		;60 MM MB LOESCHEN
;-----------------------------------------------------------0200
	LD	A,70
	CALL	KU.WART
;-----------------------------------------------------------0210
	XOR	A
	LD	(KA.Z1),A	;Z1=0
	JR	FEHL5-#
	END	

PN KP
TITL 'PROGRAMMIEREN U855 AUF AKB'

;************************************************************
;************************************************************
;**               **                                       **
;**   BAMOS       **   KASSETTENMAGNETBAND                 **
;**               **                                       **
;************************************************************
;** STAND:      01.01.81       OS-VERSION:   V1.2          **
;** BEARBEITER: HILDEBRANDT                                **
;************************************************************
;** PROGRAMMIEREN DER PIO'S AUF DER AKB                    **
;************************************************************

;ANSCHLUSSBEDINGUNGEN:
;ANFANGSADRESSE E/A-TABELLE IN IX
;BASISADRESSE AKB WIRD AUS E/A-TABELLE UEBERNOMMEN
;REGISTER AF,BC,HL WERDEN ZERSTOERT
;NACH DEM INITIALISIEREN SIND BEIDE U 855 FUER INT. GESTPERRT
INIT:LD C,(IX+2);BASISADRESSE
INC C
INC C;ADRESSE KOMMANDO TOR A
LD B,3
LD HL,INI1A
DI 
OTIR 
INC C;ADRESSE KOMMANDO TOR B
LD B,3
OTIR 
INC C
INC C
INC C
LD B,5
OTIR 
INC C
LD B,5
OTIR 
EI 
DEC C
DEC C
DEC C
LD A,3;GRUNDZUSTAND STEUERLEITUNGEN
OUT A
INC C
XOR A
OUT A
RET 
;STEUERWORTE ZUR INITIALISIERUNG AKB
;STEUERWORTE BAUSTEIN 1 TOR A-AUSGABEKANAL
INI1A:DB L(KT.IAUS);INTERRUPTVEKTOR AUSGABEKANAL
DB 0FH;BETRIEBSART:AUSGABE
DB 03H;INTERRUPT VERBOTEN
;STEUERWORTE BAUSTEIN 1 TOR B-EINGABEKANAL
INI1B:DB L(KT.IEL);INTERRUPTVEKTOR EINGABEKANAL
DB 4FH;BETRIEBSART:EINGABE
DB 03H;INTERRUPT VERBOTEN
;STEUERWORTE BAUSTEIN 2 TOR A
INI2A:DB L(KT.IBE);INTERRUPTVEKTOR SIGNAL AEB
DB 0CFH;BETRIEBSART:BIT EIN/AUSG.
DB 80H;I/O-MASKE
DB 17H;UNTERBRECHUNGSSTEUERUNG
DB 7FH;MASKE
;STEUERWORTE BAUSTEIN 2 TOR B
INI2B:DB L(KT.IST);INTERRUPTVEKTOR STATUS
DB 0CFH;BETRIEBSART:BIT EIN/AUSG.
DB 0FDH;I/O-MASKE
DB 37H;UNTERBRECHUNGSSTEUERUNG
DB 0BFH;MASKE,NUR STATUS AUF INT.
END 
