	TITLE	23.01.86 Lade-ROM fuer Floppy 5" an AMF 5120 (200k-Disketten)
	subttl	Test-RAM-Variante

	.z80

	.phase	0
modadr	equ	90h		; Basisadresse des Test-RAM

start::	ld	sp,spber
; Speicherumschaltung initialisieren
;
	xor	a
	ld	h,a
	ld	l,a
umini0::dec	hl
	ld	a,h
	or	l
	jr	nz,umini0
	out	(modadr+3),a	; Test-RAM abschalten
	ld	b,16		; 16 Sektoren initialisieren
	ld	h,a		; Sektor  0 (1)   0fh
	ld	l,a		; Adresse 0 (10h) f0h
	ld	d,0ch
umini1::push	bc
	ld	b,l
	ld	a,h
	ld	c,modadr+2
	out	(c),a		; Adresse an Sektor zuweisen
	ld	a,d		; inaktiv/aktiv
	ld	d,0
	ld	c,modadr+0
	out	(c),a		; Attribute an Sektor zuweisen
	ld	bc,110h
	add	hl,bc
	pop	bc
	djnz	umini1
	inc	a
	out	(modadr+3),a	; Test-RAM aktivieren

; Geraete zyklisch auf Bereitschaft und System abfragen
;
	xor	a
I000E::	LD	(dev),A
	CALL	gettr0		; ist das Geraet lesebereit?
	OR	A
	JR	Z,I001D		; -> ja

I0017::	LD	A,(dev)		; naechstes Geraet
	INC	A
	JR	I000E

; BC00-FFFF laden von Spur 0/1
;
I001D::
	ld	iy,0bc00h	; Ladeadresse
	ld	de,1		; Nr. des ersten zu ladenden Sektors
	ld	b,17		; Anzahl der zu ladenden Sektoren

; 1.-16. Datensatz lesen
;
I0052::	PUSH	DE
	PUSH	BC
	CALL	lesueb
	JR	NZ,I0017	; -> naechstes Geraet abfragen
	ADD	IY,BC
	POP	BC
	POP	DE
	INC	DE
	DJNZ	I0052

; es ist alles geladen
;
	CALL	allaus
	ld	a,(0bc01h)	; pruefen, ob ein System geladen wurde
	cp	'@'
	jr	nz,I0017	; -> naechstes Geraet abfragen
	jp	0c485h		; geladenes Programm starten
;-----------------------

; Lesen und uebertragen
;   IY - Adresse auf die zu lesen ist
;   DE - abs. Sektornr., des Sektors der zu lesen ist
;
lesueb::PUSH	IY
	LD	A,(dev)
	CALL	lesen
	POP	DE		; aus Lesepuffer auf Zieladresse uebertragen
	PUSH	BC
	LDIR
	POP	BC
	OR	A
	RET
;-----------------------

; alles ausschalten
;
allaus::LD	HL,pdaus

; Ausgabe einer Port/Daten-Liste
;   HL zeigt auf Liste: Port,Datum,Port,Datum,...,0
;
outtab::LD	A,(HL)
	AND	A
	RET	Z
	LD	C,A
	INC	HL
	LD	A,(HL)
	OUT	(C),A
	INC	HL
	JR	outtab

; CRC-Berechnung
;
bercrc::ex	af,af'
	LD	A,0e5h		; CRC-Anfangswert
	LD	C,09ah
	PUSH	DE
	ex	af,af'
I00E2::	ex	af,af'
I00E3::	LD	E,A
	INC	HL
	RLCA
	RLCA
	RLCA
	RLCA
	AND	0FH
	XOR	E
	LD	E,A
	RRCA
	RRCA
	RRCA
	LD	D,A
	AND	0E0H
	XOR	(HL)
	XOR	E
	LD	E,A
	LD	A,D
	AND	1FH
	XOR	C
	LD	C,A
	LD	A,D
	RRCA
	AND	0F0H
	XOR	C
	LD	C,E
	DJNZ	I00E3
	ex	af,af'
	dec	a
	ld	b,80h
	jr	nz,I00E2
	ex	af,af'
	OR	C
	POP	DE
	RET
;-----------------------

; Lesen eines mit Sektors von soltrk,solsec
;   in DE absolute Sektornr.
;
lesen::	PUSH	DE
	call	selflo
	POP	HL

	XOR	A		; aus abs. Sektornr. Spur/Sektor machen
	LD	D,A
	LD	E,5		; Anzahl der Sektoren pro Spur
	DEC	HL
	LD	B,0FFH
I013A::	INC	B
	SBC	HL,DE
	JR	NC,I013A
	ADD	HL,DE
	INC	L
	LD	H,B
	LD	(solsec),HL

; Laufwerksbereitschaft 5"Floppy ermitteln
;
	LD	DE,0
	LD	A,0BFH		; Kopf laden
	OUT	(10H),A
I0225::	IN	A,(12H)		; meldet LW ready?
	BIT	0,A
	jr	z,I0226		; -> ja
	DEC	DE
	LD	A,D
	OR	E
	JR	NZ,I0225
	JP	I02C2		; -> nein, nicht bereit

I0226::	LD	A,(isttrk)
	LD	B,A
	LD	A,(soltrk)
	SUB	B
	JR	Z,I0241
	LD	B,A
steps::	CALL	stepin
	DJNZ	steps

I0241::	LD	A,(soltrk)
	LD	(isttrk),A

	ld	a,0bfh		; Kopf laden
	OUT	(10h),A
	LD	DE,1000H
	call	wait

	CALL	les2id

I015C::	PUSH	AF
	LD	HL,pdaus0	; abschalten, Motor laufen lassen
	CALL	outtab
	POP	AF
	LD	HL,datpuf+1
	LD	BC,1024
	RET
;-----------------------

; Geraet auf Spur 0 fahren
;  in A: LW-Nr.
;
gettr0::LD	HL,pdinit	; AMF initialisieren
	PUSH	AF
	CALL	outtab
	POP	AF
	CALL	selflo
	xor	a
	LD	(isttrk),A
	CALL	stepin
	LD	B,80
	CALL	trk0
	JP	NZ,I02C2	; -> Spur 0 nicht gefunden
	xor	a		; Geraet ist bereit zum lesen
	JR	I015C
;-----------------------

; Floppy selektieren und AMF freischiessen
;
selflo::ld	hl,seltab
	and	3
	ld	e,a
	ld	d,0
	add	hl,de
	LD	A,0FFH
	OUT	(10H),A
	LD	A,0
	OUT	(14H),A
	IN	A,(16H)
	LD	A,(HL)
	OUT	(18H),A		; select Floppy
	ret
;-----------------------

trk0::	IN	A,(12h)
	AND	80h
	RET	Z
	LD	HL,D038F
	CALL	stepou
	DJNZ	trk0
	INC	B
	RET
;-----------------------

stepin::LD	HL,D0388
stepou::CALL	outtab
	LD	DE,600h

wait::	DEC	DE
	LD	A,D
	OR	E
	JR	NZ,wait
	RET
;-----------------------

; Lesen eines Sektor-ID und eines Daten-ID nach sidpuf und datpuf
;
les2id::LD	E,0FFH
I0264::	LD	C,16H		; Datenport
	LD	D,1bh		; Gaplaenge
	LD	HL,sidpuf
	LD	B,7
	ld	a,1		; 1x7 byte lesen
	PUSH	DE
	LD	DE,900H
	CALL	mrksu		; ID lesen
	POP	DE
	CP	0FEH		; Sektor-ID?
	JR	NZ,I02BB	; -> nein

	LD	B,D
I027D::	IN	A,(C)		; gap ueberlesen
	NOP
	NOP
	DJNZ	I027D

	LD	HL,datpuf
	LD	B,83H
	ld	a,8		; 8x80h byte
	PUSH	DE
	LD	DE,250H
	CALL	mrksu		; ID lesen
	POP	DE
	CALL	setber

	LD	HL,sidpuf-1	; Sektor-ID-CRC richtig?
	LD	B,7
	ld	a,1		; 1x80h
	CALL	bercrc
	JR	NZ,I02BE	; -> nein, weiter versuchen

	LD	A,(sidpuf+1)	; richtige Spurnr.?
	LD	HL,soltrk
	CP	(HL)
	JR	NZ,I02C2	; -> nein, abbrechen

	LD	A,(sidpuf+3)	; richtige Sektornr.?
	DEC	HL
	CP	(HL)
	JR	NZ,I02BE	; -> nein, weiter versuchen

	LD	HL,datpuf-1	; Daten-ID-CRC richtig?
	LD	B,83H
	ld	a,8		; 8x80h
	CALL	bercrc
	JR	NZ,I02BE	; -> nein, weiter versuchen

	xor	a		; alles ok.
	ret

I02BB::	CALL	setber
I02BE::	DEC	E
	JR	NZ,I0264

I02C2::	CALL	allaus
	LD	A,0FFH
	JP	I015C
;-----------------------

; Floppy ruecksetzen (bereit machen)
;
setber::LD	A,0A5H
	OUT	(10H),A
	LD	A,0BBH
	OUT	(10H),A
	IN	A,(16H)
	RET
;-----------------------

; Marke suchen (FE/FB) und Daten dahinter lesen
;   in  DE - timeout-Zeit beim Markensuchen
;       HL - Pufferadresse
;   out A  - Marke
;
mrksu::	ex	af,af'
	LD	(HL),0
	CALL	setber
	LD	A,85H		; Markenerkennung ein
	OUT	(10H),A
I0318::	DEC	D		; auf Marke warten
	JR	Z,I0312		; -> time out; es kam keine
I031B::	DEC	E
	JR	Z,I0318
	IN	A,(12H)
	AND	2
	JR	NZ,I031B
	IN	A,(C)
	LD	D,0
	CP	0A1H
	JR	NZ,I0312	; -> es kam falsche Marke
	IN	A,(C)
	CP	0A1H
	JR	NZ,I0312	; -> es kam nach A1 nicht weiteres A1
	IN	A,(C)
	CP	0A1H
	JR	NZ,I0312	; -> es kam nicht das 3. A1

	IN	D,(C)		; FE/FB lesen
	LD	(HL),D
	INC	HL
	ini
	ex	af,af'
I030D::	nop
I030E::	INI			; Datensatz einlesen
	JR	NZ,I030D
	ld	b,80h
	ini
	dec	a
	jr	nz,I030E
I0312::	LD	A,D
	RET

;-----------------------

; Befehle fuer Schrittausfuehrung
;
D0388::	defb	10H,0bfh
	defb	10H,03fh
	defb	10H,0bfh
	defb	0

D038F::	defb	10H,9fh
	defb	10H,1fh
	defb	10H,9fh
	defb	0

seltab::defb	0EEH,0DDH,0BBH,77H

pdinit::defb	15H,0FH		 ; AMF initialisieren
	defb	17H,4FH
	defb	11H,0FH
	defb	10H,0FFH
	defb	13H,0CFH
	defb	13H,0F7H
	defb	12H,0F7H
	defb	18H,0FFH
	defb	16H,0FFH
	defb	14H,0
	defb	0

pdaus::	defb	18H,0FFH	; alles ausschalten
pdaus0::defb	10H,0FFH
	defb	12H,0FFH
	defb	0

	.dephase

; RAM-Teil des Urladers
; =====================
;
	.phase	0c00h

; Stackbereich
;
	defs	100h,0ffh
spber::	defs	0

; Arbeitsbereiche
;
dev::	defb	0	; 0,3-Kassette; 1-5"Floppy; 2-8"Floppy
solsec::defb	0	; Soll-Spurnr.
soltrk::defb	0	; Soll-Sektornr.
isttrk::defb	0

sidpuf::defs	8,0	; Bereich fuer zu lesendes Sektor-ID

	.dephase

	.phase	8000h
datpuf::defs	1027,0	; Bereich fuer zu lesendes Daten-ID
	.dephase

	END
